buildscript {
    repositories {
        jcenter()
    }
}
plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'maven'


allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'java'

    group = 'org.amv.trafficsoft'
    version = '0.0.4-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

subprojects {
    apply plugin: 'com.jfrog.bintray'

    tasks.withType(Jar).all { jarTask ->
        manifest {
            attributes(
                    'Artifact': project.name,
                    'Group': rootProject.group,
                    'Build-Version': rootProject.version
            )
        }
    }
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
    artifacts {
        archives javadocJar, sourcesJar
    }

    if (project.hasProperty('release')) {
        bintray {
            user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            configurations = ['archives']
            pkg {
                repo = 'amv-trafficsoft-rest'
                name = project.name
                userOrg = user
                licenses = ['MIT']
                vcsUrl = 'https://github.com/amvnetworks/amv-trafficsoft-rest.git'
                labels = ['amv']
                publicDownloadNumbers = false
                version {
                    name = rootProject.version
                    desc = project.description
                    vcsTag = rootProject.version
                }
            }
        }
    }
}
